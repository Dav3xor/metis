label         : /[a-zA-Z_][a-zA-Z0-9_]*/ ;

string        : /"(\\.|[^"])*"/ ;


unsigned      : /[0-9]+/ ;

integer       : /[-][0-9]+/ 
              | /[0-9]+/ ;

float         : /[-][0-9]+ /
              | /[0-9]+ /
              | /[-][0-9]+[.][0-9]* / 
              | /[0-9]+[.][0-9]* /;

bool          : "true" 
              | "false";

vector        : '|' <float>+ '|' ;

matrix        : <vector> <vector>+ ;

fcall         : <label> <lexp>? (',' <lexp>)*;

factor        : '(' <lexp> ')'
              | <label>
              | '-' <fcall>
              | <string>
              | <unsigned>
              | <integer>
              | <float>
              | <vector>
              | <matrix>;

term          : <factor> (('*' | '/' | '%' | "dot" | "cross") <factor>)* ;

lexp          : <term> (('+' | '-') <term>)* ;

type          : ("string" | "bool" | "unsigned" | "integer" | "float" | "label" | "vector" | "matrix") ;

typeident     : <type> <label> ;

comparator    : (">" | "<" | "<=" | ">=" | "=") ;

exp           : <lexp> <comparator> <lexp>;

assignment    : <typeident> ":=" <lexp>;

function      : "def" <label> <args> ("<-" <type>) ':' <stmt>* "end";

stmt          : "if" <exp> <stmt>* ("else if" <exp> <stmt>*)* ("else" <stmt>*)? "end"
              | "while" <exp> <stmt>* "end"
              | <function>
              | "<-" <lexp> 
              | "<-|" <label>
              | <assignment> '.'
              | <fcall> '.';

args          : <typeident>? (',' <typeident>)*;



metis         : /^/ <stmt>* /$/;



