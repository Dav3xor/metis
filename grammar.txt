label         : /[a-zA-Z_][a-zA-Z0-9_]*/ ;

string        : /"(\\.|[^"])*"/ ;


unsigned      : /[0-9]+/ ;

integer       : /[-+]?[0-9]+/ ;

float         : /[-+]?([0-9]*[.])?[0-9]+/ ;

bool          : "true" 
              | "false";

vector        : '|' <float>+ '|' ;

matrix        : <vector> <vector>+ ;

fcall         : <label> <lexp>? (',' <lexp>)*;

factor        : '(' <lexp> ')'
              | <label>
              | '{' <fcall> '}'
              | <string>
              | <float>
              | <unsigned>
              | <integer>
              | <matrix>
              | <vector>;

term          : <factor> (('*' | '/' | '%' | "dot" | "cross") <factor>)* ;

lexp          : <term> (('+' | '-') <term>)* ;

type          : ("string" | "bool" | "unsigned" | "integer" | "float" | "label" | "vector" | "matrix") ;

typeident     : <type> <label> ;

comparator    : (">" | "<" | "<=" | ">=" | "=") ;

exp           : <lexp> <comparator> <lexp>;

assignment    : <typeident> ":=" <lexp>;

reserved      : ("if"|"else if"|"else"|"while"|"fin"|"string"|"bool"|"unsigned"|
                 "integer"|"float"|"label"|"vector"|"matrix"|"dot"|"cross") ;

function      : "def" <label> <args> ("<-" <type>) ':' (<block>|<stmt>)*;

block         : ("if" <exp> (<block>|<stmt>)* ("else if" <exp> (<block>|<stmt>)*)* ("else" (<block>|<stmt>)*)? 
              |  "while" <exp> (<block>|<stmt>)* 
              |  <function> ) "fin" ;
               
stmt          : ("<-" <lexp> 
              |  "<-|" <label>
              |  <assignment>
              |  <fcall> ) '.' ;

bs            : (<block> | <statement>) ;

args          : <typeident>? (',' <typeident>)*;



metis         : /^/ (<block>|<stmt>)* /$/;



